use std::str::from_utf8_unchecked;
// Pass, etc.
use rendergraph::compiler::syntax::*;
// Metadata, Struct, Type
use rendergraph::types::*;
use gfx::PrimitiveTopology;
use std::str::FromStr;

grammar;

// TODO parse floats, function definitions

Comma<T>: Vec<T> = { // (0)
    <v:(<T> ",")*> <e:T?> => match e { // (1)
        None=> v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Num: i32 = <s:r"[0-9]+"> => i32::from_str(s).unwrap();

pub Pass: Box<Pass> = {
    "pass" "{" <directives:PassDirective*> "}" => Box::new(Pass { directives })
};

PrimitiveTopology: PrimitiveTopology = {
    "triangle" => PrimitiveTopology::Triangle,
    "line" => PrimitiveTopology::Line,
    "point" => PrimitiveTopology::Point,
    "patch" => PrimitiveTopology::Patch
};

OnOffBool: bool = {
    "on" => true,
    "off" => false
};

pub TypeKeyword: Type = {
    // Primitive types
    "float" => TYPE_FLOAT,
    "int" => TYPE_INT,
    "sampler2D" => TYPE_SAMPLER2D,
    // Vector types
    "vec2" => TYPE_VEC2,
    "vec3" => TYPE_VEC3,
    "vec4" => TYPE_VEC4,
    "ivec2" => TYPE_IVEC2,
    "ivec3" => TYPE_IVEC3,
    "ivec4" => TYPE_IVEC4,
    // Matrix types
    "mat2" => TYPE_MAT2,
    "mat3" => TYPE_MAT3,
    "mat4" => TYPE_MAT4,
};

pub TypeSpecifier: Type = {
    // Base
    <t:TypeKeyword> => t,
    // Type ref
    <t:Identifier> => Type::Struct(t),
    // Array type
    <t:TypeSpecifier> "[" <a:Num> "]" => Type::Array(Box::new(t), a as usize),
};

MetadataArg: Value = {
    <Identifier> => Value::Ident(<>),
    // TODO
};

pub MetadataArgs : Vec<Value> = {
    "(" <a:Comma<MetadataArg> > ")" => a,
    () => Vec::new(),
};

pub Metadata: Metadata = {
    "@" <m:Identifier> <a:MetadataArgs> => Metadata::Custom(m,a),
};

pub Identifier: String = {
    r"[[:alpha:]]\w*" => <>.to_owned()
};

pub StructMember: StructMember = {
    <metadata:Metadata*> <ty:Identifier> <name:Identifier> ";" => StructMember { ty, name, metadata },
};

pub Struct: Struct = {
    <md:Metadata*> "struct" <n:Identifier> "{" <m:StructMember*> "}" => {
        Struct {
            name: n,
            members: m,
            metadata: md,
        }
    }
};

pub ShaderEntryPointName: String = {
    <Identifier>
};

pub PassDirective: PassDirective = {
    "primitive_topology" <topo:PrimitiveTopology> ";" => PassDirective::PrimitiveTopology(topo),
    "depth_test" <enabled:OnOffBool> ";" => PassDirective::DepthTest(enabled),
    "vertex" <n:ShaderEntryPointName> ";" => PassDirective::VertexShader(n),
    "fragment" <n:ShaderEntryPointName> ";" => PassDirective::FragmentShader(n),
    "tess_control" <n:ShaderEntryPointName> ";" => PassDirective::TessControlShader(n),
    "tess_eval" <n:ShaderEntryPointName> ";" => PassDirective::TessEvalShader(n),
    "geometry" <n:ShaderEntryPointName> ";" => PassDirective::GeometryShader(n),
    "compute" <n:ShaderEntryPointName> ";" => PassDirective::ComputeShader(n),
};

FunctionName: String = { <Identifier> };
ParameterName: String = { <Identifier> };

ParameterQualifier: () = {
    "in" => (),
    "out" => (),
    "inout" => (),
    () => ()
};

Parameter = { <ParameterQualifier> <TypeSpecifier> <ParameterName> <Metadata*> };
ParameterList = { <Comma<Parameter>> };

pub FunctionDeclaration = {
    <Metadata*> <TypeSpecifier> <FunctionName> <ParameterList>
};